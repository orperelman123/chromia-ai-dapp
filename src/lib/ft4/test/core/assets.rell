@test module;

import lib.ft4.admin;
import lib.ft4.assets;
import lib.ft4.accounts;
import crosschain_admin: lib.ft4.admin.crosschain;
import ^.auth.{ admin_priv_key };

/**
 * Registers a new asset on the blockchain with the provided info. The newly created asset entity
 * is then returned. Note however that the initial balance is alway 0 so to actually use this newly
 * created asset, you will have to mint some of it.
 * 
 * @see mint
 * 
 * @param name      the name of the asset
 * @param symbol    symbol or ticker of the asset
 * @param decimals  how many decimals to use for this asset
 * @param icon_url  optional url to an icon for this asset
 * @param type      the type of asset, defaults to `assets.ASSET_TYPE_FT4`
 * 
 * @return the newly created asset entity
 */
function create_asset(name: text, symbol: text, decimals: integer, icon_url: text = "", type: text = assets.ASSET_TYPE_FT4) {
    admin.external.register_asset_with_type(
            name,
            symbol,
            decimals,
            icon_url,
            type
        )
        .sign(admin_priv_key())
        .run();

    return assets.asset @ { .id == (name, chain_context.blockchain_rid).hash() };
}

/**
 * Registers a new crosschain asset on the blockchain with the provided info. The newly created asset entity
 * is then returned. Note that the initial balance of this asset will be 0, and since it is a crosschain
 * asset, it is not possible to directly mint more of it. To create more of it, one must simulate an incoming 
 * crosschain transfer.
 * 
 * @see mint
 * 
 * @param name                      the name of the asset
 * @param symbol                    symbol or ticker of the asset
 * @param decimals                  how many decimals to use for this asset
 * @param issuing_blockchain_rid    the rid of the issuing chain
 * @param icon_url optional url     to an icon for this asset
 * @param origin_blockchain_rid     the rid of the origin chain, often times the same value as issuing chain, but not always.
 * 
 * @return the newly created asset entity
 */
function create_crosschain_asset(
    name,
    symbol: text,
    decimals: integer,
    issuing_blockchain_rid: byte_array,
    icon_url: text,
    origin_blockchain_rid: byte_array
) {
    val id = (name, issuing_blockchain_rid).hash();
    crosschain_admin.external.register_crosschain_asset(
            id,
            name,
            symbol,
            decimals,
            issuing_blockchain_rid,
            icon_url,
            assets.ASSET_TYPE_FT4,
            x"",            
            origin_blockchain_rid
        )
        .sign(admin_priv_key())
        .run();

    return assets.asset @ { .id == (name, issuing_blockchain_rid).hash() };
}

/**
 * Mints new tokens of the provided asset into the provided account
 * 
 * @param account   the account that will receive the newly minted assets
 * @param asset     the asset to mint new tokens for
 * @param amount    how many tokens to mint
 */
function mint(accounts.account, assets.asset, amount: big_integer) {
    admin.external.mint(
            account.id,
            asset.id,
            amount
        )
        .sign(admin_priv_key())
        .run();
}

